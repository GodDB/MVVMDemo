def hasLibraryPlugin = pluginManager.hasPlugin("com.android.library")
def hasApplicationPlugin = pluginManager.hasPlugin("com.android.application")

if (hasLibraryPlugin || hasApplicationPlugin) {

    android {
        compileSdkVersion App.compileSdk
        buildToolsVersion App.buildToolsVersion

        defaultConfig {
            if(hasApplicationPlugin) {
                applicationId App.appId
            }
            targetSdkVersion App.targetSdk
            minSdkVersion App.minSdk
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

            if (hasLibraryPlugin) {
                consumerProguardFiles("consumer-rulse.pro")
            }
        }

        buildTypes {
            debug {
                if(hasApplicationPlugin){
                    shrinkResources false
                }
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }

            release {
                if(hasApplicationPlugin){
                    shrinkResources true
                }
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}